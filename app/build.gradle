apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.mvvm.demo"
        minSdkVersion 22
        targetSdkVersion 28
        versionCode buildVersionCode()
        versionName buildVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField 'int', 'DATABASE_VERSION', '1'
        buildConfigField 'String', 'DATABASE_NAME', '"com.mvvm.demo.room"'
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        sourceSets {
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        }
        ext.betaDistributionReleaseNotesFilePath = 'releasenotes'
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "environment"

    productFlavors {

        development {
            dimension "environment"
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "MVVM DEMO (DEV)"
            buildConfigField 'String', 'API_BASE_URL', '"http://algo-api-dev.lionparcel.com/v1/"'
            buildConfigField 'String', 'API_MAGENTO_URL', '"http://shop-stg.lionparcel.com/rest/"'
            versionCode buildVersionCode("dev")
            versionName buildVersionName("dev")
        }

        production {
            dimension "environment"
            resValue "string", "app_name", "MVVM DEMO"
            buildConfigField 'String', 'API_BASE_URL', '"https://algo-api.lionparcel.com/v1/"'
            buildConfigField 'String', 'API_MAGENTO_URL', '"http://shop.lionparcel.com/rest/"'
        }
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android X
    implementation "androidx.core:core-ktx:$kotlin_core_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
    implementation "android.arch.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "android.arch.navigation:navigation-ui-ktx:$navigation_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.multidex:multidex:$multidex_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_logging_version"

    // Rx binding
    implementation "com.jakewharton.rxbinding2:rxbinding:$rxbinding_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:$rxbinding_version"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"
    implementation "jp.wasabeef:glide-transformations:$glide_transformation_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Stetho
    implementation "com.facebook.stetho:stetho:$stetho_version"

    // Chuck
    debugImplementation "com.readystatesoftware.chuck:library:$chuck_version"
    releaseImplementation "com.readystatesoftware.chuck:library-no-op:$chuck_version"

    // Test
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "com.nhaarman:mockito-kotlin:$mockito_kotlin_version"

    // Android Test
    androidTestImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation "androidx.test:runner:$test_version"
    androidTestImplementation "androidx.test:rules:$test_version"
    androidTestImplementation "androidx.test.ext:junit:$ext_junit_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    androidTestImplementation "com.nhaarman:mockito-kotlin:$mockito_kotlin_version"

    // Crypto
    implementation "com.yakivmospan:scytale:$scytale_version"

    // Paging
    implementation "android.arch.paging:runtime:$paging_version"
    testImplementation "android.arch.paging:common:$paging_version"
    implementation "android.arch.paging:rxjava2:$rxjava_paging_version"
}
